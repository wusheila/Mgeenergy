<% @LANGUAGE="VBSCRIPT" CODEPAGE="65001" %>
<% OPTION EXPLICIT %>

<%
	<!--ENTER PAGE TITLE BETWEEN QUOTES BELOW-->
	Dim strTitle, strDescription
    strTitle = "Dividends"
    strDescription = "This is the description."
%>

<!-- #INCLUDE virtual="includes/head.htm" -->
<script language="JavaScript" type="text/javascript" src="js/div-calc.js"></script>
<%
    ' -- objDividend traps for the objects at <level_1>
    ' -- strDivYear captures the Year for the current dividend
    ' -- strDivExDate captures the Ex Date for the current dividend
    ' -- strDivRecord captures the Record date for the current dividend
    ' -- strDivPayable captures the Payable date for the current dividend
    ' -- intDivAmount captures the Amount of the current dividend
    ' -- strLastYear keeps the Year for the last dividend
    ' -- intDivTotal keeps a running total of this year's dividend amounts
    ' -- intRecentDividend captures the Amount of the most recent dividend
    Dim objDivXML
    Dim objDividend, strDivYear, strDivExDate, strDivRecord, strDivPayable, intDivAmount
    Dim strLastYear, intDivTotal, intRecentDividend
    
    strLastYear = "0000"
    intDivTotal = 0
    intRecentDividend = -1
    
    ' -- create parser object, prevent access to the document while it is in process of being changed or updated
    Set objDivXML = Server.CreateObject("Microsoft.XMLDOM")
    objDivXML.async = FALSE
    
    ' -- load XML from the string
    objDivXML.Load(Server.MapPath("/xml/dividend-history.xml"))
    
    ' -- verify that the XML is valid
    If objDivXML.parseError.errorCode <> 0 Then
        Response.Write("ERROR - " & objDivXML.parseError.errorCode & ", " & objDivXML.parseError.reason & "<br /><br />" & objDivXML.parseError.line & ", " & objDivXML.parseError.linepos)
        Response.End
    End If
    
    intRecentDividend = CDbl(objDivXML.documentElement.childNodes(0).text)
%>

<!-- #INCLUDE VIRTUAL="includes/render-chart.htm" -->

<body>
<!-- #INCLUDE virtual="includes/loader.htm" -->

<!-- #INCLUDE virtual="includes/header.htm" -->

<!-- #INCLUDE virtual="includes/alerts-all.htm" -->

<section id="mainc" class="clearfix">
	<div class="wrapper">
    
    	<h2><%=strTitle%></h2>
        
        <p>Shareholders have come to expect strong performance and consistent growth from MGE Energy. We have increased dividends for 40 consecutive years and grown our assets by nearly eight times as we have invested to ensure reliability and take advantage of new technology.</p>
        
        <p>Madison Gas and Electric, the main regulated subsidiary of MGE Energy has maintained the highest credit rating of any investor-owned electric and gas utility in the nation from both Standard and Poor's (S&P) and Moody's. In addition, MGE Energy is consistently ranked as a Dividend Achiever nationwide by Mergent, Inc. Recognition of this financial performance reflects our steadfast commitment to shareholder value.</p>
        
        <h3>Current Dividend Data</h3>
        
        <p>Here is the most recent quarterly dividend as set by the MGE Energy Board of Directors.</p>
        
        <!--#dividendF-form -->
        <div id="dividend-form">            
            <form action="" name="frmDivCalc" method="post" onSubmit="calcValue(document.frmDivCalc,<%=intRecentDividend%>); return false;">            
            <fieldset>
            	<legend title="Most Recent Dividend"><strong>Dividend Calculator</strong></legend>            
            	<div class="rob-grid1-2 padding left">
                	<label class="left" for="shares" title="Number of shares">Number of shares:</label> 
                    <div class="clearfix"></div>             
                        <input name="shares" id="shares" tabindex="5" title="Enter number of shares here" onfocus="this.select();" maxlength="12" />
                     <span class="font-large">x</span> <label for="dividend" title="Most recent dividend"><span class="font-small"><sup>$</sup></span><%=intRecentDividend%></label> 
                    = <div id="divValue" class="font-large right"></div>
               </div>
            	<div class="clearfix"></div>
            	<div class="padding left"><input name="Submit" type="button" tabindex="6" onclick="calcValue(document.frmDivCalc,<%=intRecentDividend%>); return false;" value="Calculate" class="submit" /></div>            
            </fieldset>
            </form>
		</div><!--#dividendF-form -->
        
        <h4>Divident History</h4>
        
        <p>MGE Energy embraces our obligation to equitably serve customers with reliable energy while producing long-term value for shareholders.</p>

        <!--chart data-->
        <div id="dividend-chart" class="rob-grid1 margin-top">
            <%
              Call renderChartJS("Column2D", "xml/dividend-data.xml", "", "DividendData", "100%", 500, "")
            %>
        </div><!--chart data-->

		<!--table--dividend data-->
        <div class="rob-grid1 margin-top">
        <table>
            <thead>
            	<th>Ex-Date</th>
                <th>Record</th>
                <th>Payable</th>
                <th>Amount</th>
            </thead>
            <tbody>
                <%
                ' -- retrieve all of the nodes
                For Each objDividend In objDivXML.documentElement.childNodes
                  ' -- parse out the item elements
                  intDivAmount = CDbl(objDividend.Text)
                  strDivYear = objDividend.getAttribute("year")
                  strDivExDate = objDividend.getAttribute("exdate")
                  strDivRecord = objDividend.getAttribute("record")
                  strDivPayable = objDividend.getAttribute("payable")
                
                  If strLastYear = "0000" Then
                    strLastYear = strDivYear
                  End If
                
                  If strDivYear <> strLastYear Then
                %>
                    <tr class="div-total">
                      <td colspan="3"><strong>Total dividends paid per share in <%=strLastYear%></strong></td>
                      <td><strong><span class="font-small"><sup>$</sup></span> <%=FormatNumber(intDivTotal,4)%></strong></td>
                    </tr>
                <%
                    strLastYear = strDivYear
                    intDivTotal = 0
                  End If
                
                  intDivTotal = intDivTotal + CDbl(intDivAmount)
                %>
                    <tr>
                      <td class="div-ex-date"><%=strDivExDate%></td>
                      <td class="div-record"><%=strDivRecord%></td>
                      <td class="div-payable"><%=strDivPayable%></td>
                      <td class="div-amount"><span class="font-small"><sup>$</sup></span> <%=FormatNumber(intDivAmount,4)%></td>
                    </tr>
                <%
                Next
                
                ' -- clear the XML object
                Set objDivXML = Nothing
                
                   If intDivTotal <> 0 Then
                %>
                <tr class="div-total">
                  <td colspan="3"><strong>Total dividends paid per share in <%=strLastYear%></strong></td>
                  <td><strong><span class="font-small"><sup>$</sup></span> <%=FormatNumber(intDivTotal,4)%></strong></td>
                </tr>
            <% End If %>
            </tbody>
        </table>
        </div><!--table--dividend data-->
               
    </div><!--.wrapper-->
</section><!--#mainc-->

<!-- #INCLUDE virtual="includes/footer.htm" -->

</body>
<!-- #INCLUDE virtual="includes/bottom-js.htm" -->
</html>